>D
; enable sending telemetry data
entel=1

; Modbus task and TCP server running
runMB=0;

; minimum energy threshold in Wh to discard empty readings
minE=10000

; variables SML
pcurr=0

; variables Modbus TCP
res=0
cnt=0
xcnt=0
reg=0
nval=0
dform=0
ecnt=0
ctime=0

vsml1=0
vsml2=0
vsml3=0

M:req=0 12
M:resp=0 9
M:data=0 66

; variables Modbus serial
Ap=0
Ae=0
Bp=0
Be=0
Cp=0
Ce=0
Dp=0
De=0
Ep=0
Ee=0
Fp=0
Fe=0
Gp=0
Ge=0
Hp=0
He=0

; variables Domoticz
usage=0
ret=0
cons=0
prod=0

>B
=>otaurl !!NEVER UPGRADE VIA WEB SERVER!!
=>sensor53 r

; on boot disable publishing MQTT at teleperiod interval
smlj=0

>BS
; init LED
spinm(2 1)

; set Modbus TCP register values
#dosel
data[0]=0
; default data type = word
dform=1
switch reg

    case 768
        data={0}

    case 1706
        data={0}

    case 40000
        data={21365 28243}

    case 40002
        data={1 65}

    case 40004
        data={70 114 111 110 105 117 115 0 0 0 0 0 0 0 0 0}
        data={83 109 97 114 116 32 77 101 116 101 114 32 54 51 65 0}
        data={0 0 0 0 0 0 0 0}
        data={0 0 0 0 0 0 0 0}
        data={48 48 48 48 48 48 48 49 0 0 0 0 0 0 0 0}
        data={240 213}

    case 40069
        data={213 124}

    case 40071
        data={0 0 0 0 0}
        data={0 0 0 0 0}
        data={0 0 0 vsml1 0}
        data={0 0 0 0 0}
        data={0 0 0 0 0}
        data={0 0 0 0 0}
        dform=3
        nval=nval/2

    case 40129
        data={vsml3 0 0 0 vsml2}
        data={0 0 0 0 0}
        data={0 0 0 0 0}
        data={0}
        dform=3
        nval=nval/2

    case 40193
        data={0 0}

    case 40195
        data={65535 0}

    case 50000
        data={0 0}

; current power in W 40097 sml[1]
; total Wh exp 40129 sml[3]
; total Wh imp 40137 sml[2]

ends

>t1
; check incomming data
;ctime=millis
xcnt =wsa()
;print %xcnt%
if xcnt>=12 {    
    ; check if SML readings are valid (>minE), else discard request
    if (vsml2>minE and vsml3>minE and xcnt<=120) {

            res=wsra(req)
            reg=(req[9]<<8)|req[10]
            nval=req[12]
;            print read register: %reg%, count: %nval%

            ; transaction id
            resp[1]=req[1]
            resp[2]=req[2]
            ; protocol id
            resp[3]=0
            resp[4]=0
            ; length
            resp[5]=0
            resp[6]=req[12]*2+3
            ; device address
            resp[7]=req[7]
            ; function code
            resp[8]=req[8]
            ; payload len
            resp[9]=req[12]*2

            ; write response header
            wswa(resp 9)

            ; write response data
            =#dosel
            wswa(data nval dform)
 
    } else {
        ecnt=ecnt+1
	print clear TCP buffer xcnt: %xcnt% ecnt: %ecnt%
        wsf()
    }
; print time: %(millis-ctime)%
}

>F
vsml1=sml[1]
vsml2=sml[2]*1000
vsml3=sml[3]*1000

>T
usage=sml[2]*1000
ret=sml[3]*1000
cons=sml[1]
prod=0
if cons<0 {
	prod=abs(cons)
	cons=0
}

Ap=MOD#A_power
Ae=MOD#A_energy*1000
Bp=MOD#B_power
Be=MOD#B_energy*1000
Cp=MOD#C_power
Ce=MOD#C_energy*1000
Dp=MOD#D_power
De=MOD#D_energy*1000
Ep=MOD#E_power
Ee=MOD#E_energy*1000
Fp=MOD#F_power
Fe=MOD#F_energy*1000
Gp=MOD#G_power
Ge=MOD#G_energy*1000
Hp=MOD#H_power
He=MOD#H_energy*1000

>S
; start TCP server and Modbus task t1 after wifi is connected
if (entel==1 and wifis>0 and runMB==0) {
    res=wso(502)
    print start TCP server - return code: %res%
    ; create task 1 every 50ms on core1, priority 3, stack 8kb
    ct(1 50 1 3 8192)
    runMB=1
}

; re-enable publishing at MQTT teleperiod if data is valid (>minE)
if (smlj==0 and usage>minE and ret>minE and entel==1) {
	smlj|=1
}

; update Domoticz MQTT
if upsecs%tper==0 {
  if (usage>minE and ret>minE and entel==1) {
  	=>publish domoticz/in {"idx":2162,"svalue":"%usage%;0;%ret%;0;%cons%;%prod%"}
  }
  if (Ae>minE and entel==1) {
  	=>publish domoticz/in {"idx":770,"svalue":"%Ap%;%Ae%"}
  }
  if (Be>minE and entel==1) {
  	=>publish domoticz/in {"idx":771,"svalue":"%Bp%;%Be%"}
  }
  if (Ce>minE and entel==1) {
  	=>publish domoticz/in {"idx":772,"svalue":"%Cp%;%Ce%"}
  }
  if (De>minE and entel==1) {
  	=>publish domoticz/in {"idx":2156,"svalue":"%Dp%;%De%"}
  }
  if (Ee>minE and entel==1) {
  	=>publish domoticz/in {"idx":725,"svalue":"%Ep%;%Ee%"}
  }
  if (Fe>minE and entel==1) {
  	=>publish domoticz/in {"idx":2157,"svalue":"%Fp%;%Fe%"}
  }
  if (Ge>minE and entel==1) {
  	=>publish domoticz/in {"idx":2158,"svalue":"%Gp%;%Ge%"}
  }
  if (He>minE and entel==1) {
  	=>publish domoticz/in {"idx":2153,"svalue":"%Hp%;%He%"}
  }
}

; onboard LED on if Power_curr < 0
pcurr=sml[1]
if pcurr>0 {
    spin(2 0)
} else {
    spin(2 1)
}

>R
; close TCP server
res=wsc()

>M 2
+1,16,s,0,9600,SML
1,77070100100700ff@1,Leistung,W,Power_curr,0
1,77070100010800ff@1000,Verbrauch,kWh,Total_in,3
1,77070100020800ff@1000,Netzeinspeisung,kWh,Total_out,3
1,=h<hr/>
+2,3,M,0,9600,MOD,1,2,01030025,0103001D,02030025,0203001D,03030025,0303001D,04030025,0403001D,05030025,0503001D,06030025,0603001D,0704000C,07040048,0804000C,08040048
2,010304UUuu@i0:0.1,A Power,W,A_power,0
2,010304UUuuUUuu@i1:100,A Energy,kWh,A_energy,2
2,=h<hr/>
2,020304UUuu@i2:0.1,B Power,W,B_power,0
2,020304UUuuUUuu@i3:100,B Energy,kWh,B_energy,2
2,=h<hr/>
2,030304UUuu@i4:0.1,C Power,W,C_power,0
2,030304UUuuUUuu@i5:100,C Energy,kWh,C_energy,2
2,=h<hr/>
2,040304UUuu@i6:0.1,D Power,W,D_power,0
2,040304UUuuUUuu@i7:100,D Energy,kWh,D_energy,2
2,=h<hr/>
2,050304UUuu@i8:0.1,E Power,W,E_power,0
2,050304UUuuUUuu@i9:100,E Energy,kWh,E_energy,2
2,=h<hr/>
2,060304UUuu@i10:0.1,F Power,W,F_power,0
2,060304UUuuUUuu@i11:100,F Energy,kWh,F_energy,2
2,=h<hr/>
2,070404ffffffff@i12:1,G Power,W,G_power,1
2,070404ffffffff@i13:1,G Energy,kWh,G_energy,3
2,=h<hr/>
2,080404ffffffff@i14:1,H Power,W,H_power,1
2,080404ffffffff@i15:1,H Energy,kWh,H_energy,3
#
